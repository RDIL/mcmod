buildscript {
    repositories {
        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/forge-proxy"
        }

        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/sponge-proxy"
        }

        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/maven-central-proxy"
        }
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

configurations {
    embed
    compile.extendsFrom(embed)
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = "UTF-8"
compileJava.options.compilerArgs << "-Xlint:none"

version = "0.1.0"
group = "rocks.rdil"
archivesBaseName = "MCMod"
description = ""

String tweaker = "${project.group}.${project.archivesBaseName.toLowerCase()}.ModBootstrap"

minecraft {
    version "1.8.9-11.15.1.2318-1.8.9"
    runDir "run"
    mappings "stable_22"
    makeObfSourceJar false
}

repositories {
    maven {
        url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/sponge-proxy"
    }
}

dependencies {
    embed("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "gson"
        exclude module: "commons-io"
        exclude module: "log4j-core"
    }
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixin.${project.archivesBaseName.toLowerCase()}.refmap.json"
    disableRefMapWarning true
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "modid", project.archivesBaseName.toLowerCase()
    inputs.property "description", project.description

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        include "mixins." + project.archivesBaseName.toLowerCase() + ".json"
        expand "version": project.version,
               "mcversion": project.minecraft.version,
               "modid": project.archivesBaseName,
               "description": project.description
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
        exclude "mixins." + project.archivesBaseName + ".json"
    }
}
